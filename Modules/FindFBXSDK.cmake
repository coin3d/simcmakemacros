# Input:
#   Environment variable "FBXSDKDIR" pointing to the base dir of
#   the FBX SDK installation. e.g. c:/Program Files/Autodesk/FBXSDK-2009.3
#
# Output:
#   FBXSDK_INCLUDE_DIR
#   FBXSDK_LIBRARIES
#   FBXSDK_FOUND
#   FBXSDK_DEFINES
#

SET(FBXSDKDIR $ENV{FBXSDKDIR})
SET(FBXSDK_FOUND FALSE)

SET(FBXSDK_NAMES fbxsdk_md2008 fbxsdk_gcc4_ub fbxsdk_gcc4)
SET(FBXSDK_NAMES_DEBUG fbxsdk_md2008d fbxsdk_gcc4d)

IF (FBXSDKDIR)
  FIND_LIBRARY(FBXSDK_LIBRARY_RELEASE 
               NAMES ${FBXSDK_NAMES}
               PATHS ${FBXSDKDIR}
               PATH_SUFFIXES lib
               NO_DEFAULT_PATH)
  FIND_LIBRARY(FBXSDK_LIBRARY_DEBUG 
               NAMES ${FBXSDK_NAMES_DEBUG}
               PATHS ${FBXSDKDIR}
               PATH_SUFFIXES lib
               NO_DEFAULT_PATH)
               
  IF(FBXSDK_LIBRARY_RELEASE AND NOT FBXSDK_LIBRARY_DEBUG)
    SET(FBXSDK_LIBRARY_DEBUG ${FBXSDK_LIBRARY_RELEASE})
    SET(FBXSDK_LIBRARY       ${FBXSDK_LIBRARY_RELEASE})
    SET(FBXSDK_LIBRARIES     ${FBXSDK_LIBRARY_RELEASE})
  ENDIF()

  IF(FBXSDK_LIBRARY_DEBUG AND NOT FBXSDK_LIBRARY_RELEASE)
    SET(FBXSDK_LIBRARY_RELEASE ${FBXSDK_LIBRARY_DEBUG})
    SET(FBXSDK_LIBRARY       ${FBXSDK_LIBRARY_DEBUG})
    SET(FBXSDK_LIBRARIES     ${FBXSDK_LIBRARY_DEBUG})
  ENDIF()

  IF(FBXSDK_LIBRARY_DEBUG AND FBXSDK_LIBRARY_RELEASE)
    IF(CMAKE_CONFIGURATION_TYPES OR CMAKE_BUILD_TYPE)
      #If the generator supports configuration types then set
      #optimized and debug libraries, or if the CMAKE_BUILD_TYPE has a value
      SET(FBXSDK_LIBRARY optimized ${FBXSDK_LIBRARY_RELEASE} debug ${FBXSDK_LIBRARY_DEBUG})
    ELSE()
      #If there are no configuration types and CMAKE_BUILD_TYPE has no value
      #then just use the release libraries
      SET(FBXSDK_LIBRARY ${FBXSDK_LIBRARY_RELEASE})
    ENDIF()
    SET(FBXSDK_LIBRARIES optimized ${FBXSDK_LIBRARY_RELEASE} debug ${FBXSDK_LIBRARY_DEBUG})
  ENDIF()

  SET(FBXSDK_LIBRARY ${FBXSDK_LIBRARY} CACHE FILEPATH "The FBXSDK library")
  MARK_AS_ADVANCED(FBXSDK_LIBRARY_RELEASE FBXSDK_LIBRARY_DEBUG)

  FIND_PATH(FBXSDK_INCLUDE_DIR
            NAME fbxsdk.h 
            PATHS ${FBXSDKDIR} 
            PATH_SUFFIXES include 
            NO_DEFAULT_PATH)

  IF (FBXSDK_LIBRARY AND FBXSDK_INCLUDE_DIR)
    # OS-dependent configuration
    IF (WIN32)
      SET(FBXSDK_SYSTEM_LIBRARIES "advapi32.lib" "wininet.lib")
      SET(FBXSDK_DEFINES -DK_PLUGIN -DK_FBXSDK -DK_NODLL -DBOOST_MATH_TR1_NO_LIB)
    ELSEIF (APPLE)
      SET(FBXSDK_SYSTEM_LIBRARIES -liconv "-framework SystemConfiguration" "-framework CoreServices")
      SET(FBXSDK_DEFINES "")
    ELSE ()
      SET(FBXSDK_SYSTEM_LIBRARIES "")
      SET(FBXSDK_DEFINES "")
    ENDIF ()
    SET(FBXSDK_LIBRARIES ${FBXSDK_LIBRARY} ${FBXSDK_SYSTEM_LIBRARIES})
    SET(FBXSDK_FOUND TRUE)
    IF (NOT FBXSDK_FIND_QUIETLY)
      MESSAGE(STATUS "Found FBX SDK: ${FBXSDK_LIBRARY} with includes ${FBXSDK_INCLUDE_DIR}")
    ENDIF ()
  ELSE ()
    IF (FBXSDK_FIND_REQUIRED)
      MESSAGE(FATAL_ERROR "Could not find FBX SDK in ${FBXSDKDIR}")
    ELSE ()
      MESSAGE(STATUS "Could not find FBX SDK in ${FBXSDKDIR}")
    ENDIF ()
  ENDIF ()
ELSEIF (FBXSDK_FIND_REQUIRED)
  MESSAGE(FATAL_ERROR "Automatic find of FBX SDK not implemented. "
        "Set the environment variable FBXSDKDIR to point to the directory "
        "where the FBX SDK is located.")
ENDIF ()
